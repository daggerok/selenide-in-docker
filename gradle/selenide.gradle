/**
 * Selenide / drivers dependencies...
 */
dependencies {
  testImplementation("com.codeborne:selenide:$selenideVersion") {
    exclude module: "selenium-opera-driver"
    exclude module: "selenium-safari-driver"
    exclude module: "selenium-edge-driver"
    exclude module: "selenium-ie-driver"
    because "we don't wanna use any of these..."
  }
  ////and these are hopefully comes with selenide library as a transitive dependencies:
  //testImplementation("org.seleniumhq.selenium:selenium-chrome-driver:$seleniumDriverVersion")
  //testImplementation("org.seleniumhq.selenium:selenium-firefox-driver:$seleniumDriverVersion")
  testImplementation("com.codeborne:phantomjsdriver:$phantomjsDriverVersion")
}

static def findAllSelenideSystemProps() {
  System.properties?.findAll {
    it.key == "remote" ||
        it.key.startsWith("selenide") ||
        it.key.startsWith("webdriver")
  } ?: [:]
}

final String selenideBrowser = "selenide.browser"
//final String defaultBrowser = "phantomjs"
final String defaultBrowser = "chrome"

test {
  systemProperties = findAllSelenideSystemProps() + [
      "webdriver.chrome.verboseLogging": "true",
      "selenide.browser": System.getProperty(selenideBrowser, defaultBrowser),
  ] + (
    project.hasProperty("remote") ? [ "remote": "http://127.0.0.1:4444/wd/hub" ] : [:]
  )
}

["firefox", "chrome", "phantomjs"].each { taskName ->
  Task e2eTestBrowserSelectorTask = tasks.create(name: taskName) {
    def systemProps = findAllSelenideSystemProps()
    systemProps["$selenideBrowser"] = taskName

    doFirst {
      test {
        systemProperties = systemProps
      }
    }
  }
  test.shouldRunAfter(e2eTestBrowserSelectorTask)
}

final String selenideHeadless = "selenide.headless"

task headless {
  def systemProps = findAllSelenideSystemProps()
  systemProps["$selenideHeadless"] = true

  doFirst {
    test {
      systemProperties = systemProps
    }
  }
}

task headful {
  def systemProps = findAllSelenideSystemProps()
  systemProps["$selenideHeadless"] = false

  doFirst {
    test {
      systemProperties = systemProps
    }
  }
}

test.shouldRunAfter headless, headful
